plugins {
    id "dev.architectury.loom" version "1.6-SNAPSHOT" apply false
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = project.minecraft_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        archivesName = "${rootProject.modid}-${rootProject.minecraft_version}-${project.name}"
    }

    def modLoader = project.name
    def isCommon = (modLoader == "common")

    repositories {
        maven { url "https://maven.shedaniel.me" }
        maven { url "https://cursemaven.com" }
        maven { url "https://maven.terraformersmc.com/releases" }
        maven { url "https://maven.parchmentmc.org" }
        maven { url "https://maven.neoforged.net/releases" }
        maven { url "https://maven.flashyreese.me/snapshots" }
        maven { url "https://api.modrinth.com/maven" }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_mapping_version}@zip")
        }

        modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:${rootProject.rei_version}"
        modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin:${rootProject.rei_version}"
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    if (!isCommon) {

        publishing {
            publications {
                maven(MavenPublication) {
                    artifactId = "stellaris-${modLoader}-${rootProject.minecraft_version}"
                    from components.java

                    pom {
                        name = "Stellaris ${modLoader}"
                        url = "https://github.com/st0x0ef/stellaris"
                    }
                }
            }
            repositories {
                maven {
                    url = "https://maven.odysseyus.fr/releases/"
                    credentials {
                        username = System.getenv("MAVEN_USER")
                        password = System.getenv("MAVEN_PASS")
                    }
                }
            }
        }

    }
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}
